plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

// Get git info from dart-define for reproducible builds
def gitCommitHash = project.hasProperty('dart-defines') ? 
    project.getProperty('dart-defines').split(',').find { it.startsWith('GIT_COMMIT_HASH=') }?.split('=', 2)[1] : 'unknown'
def gitBranch = project.hasProperty('dart-defines') ? 
    project.getProperty('dart-defines').split(',').find { it.startsWith('GIT_BRANCH=') }?.split('=', 2)[1] : 'unknown'
def gitIsDirty = project.hasProperty('dart-defines') ? 
    project.getProperty('dart-defines').split(',').find { it.startsWith('GIT_IS_DIRTY=') }?.split('=', 2)[1] : 'unknown'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 36
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "com.gooseberrydevelopment.pinepods"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 36
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        
        // Add git commit info for reproducible builds and vcsInfo
        buildConfigField "String", "GIT_COMMIT_HASH", "\"${gitCommitHash}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitBranch}\""
        buildConfigField "String", "GIT_IS_DIRTY", "\"${gitIsDirty}\""
        
        // Set VCS info for reproducible builds (this helps with META-INF/version-control-info.textproto)
        manifestPlaceholders.putAll([
            gitCommitHash: gitCommitHash,
            gitBranch: gitBranch,
            gitIsDirty: gitIsDirty
        ])
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs (for IzzyOnDroid/F-Droid)
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles (for Google Play)
        includeInBundle = false
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            shrinkResources false
        }

        release {
            signingConfig signingConfigs.release
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    namespace 'com.gooseberrydevelopment.pinepods'

    lint {
        abortOnError false
        disable 'InvalidPackage'
    }

    // Disable PNG crunching for reproducible builds
    aaptOptions {
        cruncherEnabled = false
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
